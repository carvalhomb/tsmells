/**
 * This file is part of TSmells
 *
 * TSmells is free software; you can redistribute it and/or modify it 
 * under the terms of the GNU General Public License as published by the 
 * Free Software Foundation; either version 2 of the License, or (at your 
 * option) any later version.
 *
 * TSmells is distributed in the hope that it will be useful, but WITHOUT 
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS 
 * FOR A PARTICULAR PURPOSE. See the GNU General Public License for more 
 * details.
 *
 * You should have received a copy of the GNU General Public License along 
 * with TSmells; if not, write to the Free Software Foundation, Inc., 
 * 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA 
 *
 * Copyright 2007 Manuel Breugelmans <manuel.breugelmans@student.ua.ac.be>
 */

/**
 * Append xUnit framework checker methods!
 * Messageless TestCheckers will only be detected if they were
 * marked as such and handed to writeXTestEntities.rml
 *
 * TestEntities: + writeCppTestEntities.rml
 *               + writeJavaTestEntities.rml
 * 
 * eg: appendJavaTE.sh rsf/SixAssertEqualsNoDesc.rsf  | \
 *            crocopat ${TSMELLS}/src/AssertionRoulette.rml
 *
 **/

// fake it implementation
// PRINT "MyTest.myCommand(),6,6", ENDL;
//        ^^ testcase.testcommand(), total number of checkers, number of messageless checkers

/**
 * holds test commands/helpers with message-less checker methods
 * @col id:  invoke ID
 * @col x:   test command/helper ID
 * @col y:   message-less assert ID
 */
NoDescAssert(id,x,y) := NoFailureMessage(y) & CheckerInvoke(id,x,y) & (TestCommandId(x) | TestHelperId(x));

//PRINT ["NoDescAssert"] NoDescAssert(x,y,z);
//PRINT ["TestCommand"] TestCommand(x,y);
//PRINT ["CheckerInvoke"] CheckerInvoke(x,y,z);
//PRINT ["TestHelper"] TestHelper(x,y);

FOR tc IN NoDescAssert(_,x,_) {
    // log roulette if more then 3 message-less invokes
    // TODO parameterize this
    num := #(NoDescAssert(a,tc,b));
    IF ( num >= 3 ) {
        TCName(x) := TestCommand(tc,x);
        type := "TC";
        IF (#(TCName(x)) = 0) {
            // this is a testhelper
            TCName(x) := TestHelper(tc,x);
            type := "TH";
        }
        // get the filename
        FileName(x) := EX(y, InvokableEntityBelongsToFile(tc,y,_) & File(y,x));
        // grab the linenumbers of all message-less assertions
        LineNrs(x) := EX(y, NoDescAssert(y,tc, _) & InvocationLocation(y,_,x));
        FOR n IN TCName(x) { // singleton
            total := #(CheckerInvoke(a,tc,b));
            PRINT "AssertionRoulette;", type, ";", n, ";", total, ";", num, ";";
            FOR f IN FileName(x) { // singleton
                 PRINT f;
            }
            FOR l IN LineNrs(x) {
                PRINT ";", l;
            }
            PRINT ENDL;
        }
    }
}
