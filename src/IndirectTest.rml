/**
 * This file is part of TSmells
 *
 * TSmells is free software; you can redistribute it and/or modify it 
 * under the terms of the GNU General Public License as published by the 
 * Free Software Foundation; either version 2 of the License, or (at your 
 * option) any later version.
 *
 * TSmells is distributed in the hope that it will be useful, but WITHOUT 
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS 
 * FOR A PARTICULAR PURPOSE. See the GNU General Public License for more 
 * details.
 *
 * You should have received a copy of the GNU General Public License along 
 * with TSmells; if not, write to the Free Software Foundation, Inc., 
 * 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA 
 *
 * Copyright 2007 Manuel Breugelmans <manuel.breugelmans@student.ua.ac.be>
 */

/**
 * Indirect test is somewhat fuzzy described in litarature.
 * A heuristic is used; a treshold on the number of
 * different classes a TC invokes/accesses (aka NPTU). 
 * This treshold is configurable.
 *
 * For a formal definition see formspec.pdf
 **/


/**
 * Contains the different classes a test command accesses. 
 * Not counting itself, nor other Test Commands.
 *
 * @col tcidd: testcommand id
 * @col clazz: accessed class
 **/
diffClassAccess(tcidd, clazz) := 
    EX(y_dca,
              TestCommandId(tcidd) & 
              PureAccesses(_,tcidd,y_dca) & 
              AttributeBelongsToClass(y_dca,clazz) & 
              !TestCaseId(clazz));
diffClassAccessHelper(tcidd,clazz) :=
    EX(y, thid, 
              TransComHelper(tcidd, thid) &
              PureAccess(_, thid, y) &
              AttributeBelongsToClass(y, clazz) &
              !TestCaseId(clazz));

/**
 * Contains the different production classes a test command 
 * invokes a method from.
 * @col tcidd: test command id
 * @col clazz: invoked class
 **/
diffClassInvokes(tcidd, clazz) :=
    EX(y,
              TestCommandId(tcidd) &
              PureInvokes(_, tcidd, y) &
              ProductionMethodId(y) &
              MethodBelongsToClass(y, clazz));
diffClassInvokesHelper(tcidd, clazz) :=
    EX(y, thid,
              TransComHelper(tcidd, thid) &
              PureInvokes(_, thid, y) &
              ProductionMethodId(y) &
              MethodBelongsToClass(y, clazz));

//PRINT ["diffClassAccess"] diffClassAccess(x,y);
//PRINT ["diffClassInvokes"] diffClassInvokes(x,y);

/**
 * TestCommand id's which are flagged as 
 * indirect test, as well as the NPTU
 **/
IndirectTestId("", "");

FOR tcid_idt IN TestCommandId(x) {
    nptu := #(diffClassInvokes(tcid_idt,c) | diffClassInvokesHelper(tcid_idt,c) |
              diffClassAccess(tcid_idt,c)  | diffClassAccessHelper(tcid_idt, c));
    IF ( nptu >= INDIRECT_TEST_TRESHOLD) {
        // got one, append
        IF (nptu <= COUNT_MAX) {
            IndirectTestId(x, num) := 
                IndirectTestId(x,num) |
                (
                    x = tcid_idt & 
                    ( count(STRING(nptu), num) )
                );
        } ELSE {
            // larger then the count relation. log this is as 'A LOT'
            IndirectTestId(x, num) := 
                IndirectTestId(x,num) |
                (x = tcid_idt & 
                    ( count("alot", num) )
                );
        }
    }
}

/**
 * Contains full indirect test information
 * 
 * @col tcnm: test command name
 * @col tres: treshold for ptmi
 * @col nptu: number of production type uses
 * @col file: sourcefile with tcnm's implementation
 * @col line: line this test command is defined on
 **/
IndirectTest(tcnm, tres, nptu_x, file, line) :=
    EX(tcid_idx,
            IndirectTestId(tcid_idx, nptu_x) &
            tres = "INDIRECT_TEST_TRESHOLD" &
            TestEntityInfo(tcid_idx, tcnm, file, line)
      );

PRINT ["IndirectTest"] IndirectTest(aa,bb,cc,dd,ee);
