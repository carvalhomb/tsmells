/**
 * This file is part of TSmells
 *
 * TSmells is free software; you can redistribute it and/or modify it 
 * under the terms of the GNU General Public License as published by the 
 * Free Software Foundation; either version 2 of the License, or (at your 
 * option) any later version.
 *
 * TSmells is distributed in the hope that it will be useful, but WITHOUT 
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS 
 * FOR A PARTICULAR PURPOSE. See the GNU General Public License for more 
 * details.
 *
 * You should have received a copy of the GNU General Public License along 
 * with TSmells; if not, write to the Free Software Foundation, Inc., 
 * 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA 
 *
 * Copyright 2007 Manuel Breugelmans <manuel.breugelmans@student.ua.ac.be>
 */

/**
 * Indirect test is somewhat fuzzy described in litarature.
 * A heuristic is used; a treshold on the number of
 * different classes a TC invokes/accesses (aka NPTU). 
 * This treshold is configurable.
 *
 * For a formal definition see formspec.pdf
 **/

// TODO there's something fishy with this script.
// All pieces tests pass but on real code no 
// IndirectTests get found, even with a ridiculously
// low treshold. Maybe some variable pollution 
// introduced by other scripts ???


// TODO move ComHelpers & TransComHelper to a common
// script. AssertionLess could use this information as
// well instead of recomputing a closure.
/**
 * All helpers thid invoked from command tcid
 **/
ComHelpers(tcid, thid) := 
    TestCommandId(tcid) & TestHelperId(thid) & PureInvokes(_,tcid, thid);
TestHelperInvoke(thid1, thid2) := 
    TestHelperId(thid1) & PureInvokes(_, thid1, thid2) & TestHelperId(thid2);

/**
 * All helpers thid invoked from tcid, directly & indirectly
 **/
TransComHelper(tcid, thid) :=
    ComHelpers(tcid, thid) | 
    EX(z, ComHelpers(tcid,z) & TC(TestHelperInvoke(z, thid)));

/**
 * Contains the different classes a test command accesses. 
 * Not counting itself, nor other Test Commands.
 *
 * @col tcidd: testcommand id
 * @col clazz: accessed class
 **/
diffClassAccess(tcidd, clazz) := 
    EX(y, id, TestCommandId(tcidd) & 
              PureAccesses(id,tcidd,y) & 
              AttributeBelongsToClass(y,clazz) & 
              !TestCaseId(clazz));
diffClassAccessHelper(tcidd,clazz) :=
    EX(y, thid, id,
              TransComHelper(tcidd, thid) &
              PureAccess(id, thid, y) &
              AttributeBelongsToClass(y, clazz) &
              !TestCaseId(clazz));

/**
 * Contains the different production classes a test command 
 * invokes a method from.
 * @col tcidd: test command id
 * @col clazz: invoked class
 **/
diffClassInvokes(tcidd, clazz) :=
    EX(y, id, TestCommandId(tcidd) &
              PureInvokes(id, tcidd, y) &
              ProductionMethodId(y) &
              MethodBelongsToClass(y, clazz));
diffClassInvokesHelper(tcidd, clazz) :=
    EX(y, thid, id,
              TransComHelper(tcidd, thid) &
              PureInvokes(id, thid, y) &
              ProductionMethodId(y) &
              MethodBelongsToClass(y, clazz));

//PRINT ["diffClassAccess"] diffClassAccess(x,y);
//PRINT ["diffClassInvokes"] diffClassInvokes(x,y);

/**
 * TestCommand id's which are flagged as 
 * indirect test, as well as the NPTU
 **/
IndirectTestId("", "");

FOR tcid IN TestCommandId(x) {
    nptu := #(diffClassInvokes(tcid,c) | diffClassInvokesHelper(tcid,c) |
              diffClassAccess(tcid,c)  | diffClassAccessHelper(tcid, c));
    IF ( nptu >= INDIRECT_TEST_TRESHOLD) {
        // got one, append
        IF (nptu <= COUNT_MAX) {
            IndirectTestId(x, num) := 
                IndirectTestId(x,num) |
                (
                    x = tcid & 
                    ( count(STRING(nptu), num) )
                );
        } ELSE {
            // larger then the count relation. log this is as 'A LOT'
            IndirectTestId(x, num) := 
                IndirectTestId(x,num) |
                (x = tcid & 
                    ( count(STRING(nptu), num) )
                );
        }
    }
}

/**
 * Contains full indirect test information
 * 
 * @col tcnm: test command name
 * @col tres: treshold for ptmi
 * @col nptu: number of production type uses
 * @col file: sourcefile with tcnm's implementation
 * @col line: line this test command is defined on
 **/
IndirectTest(tcnm, tres, nptu_x, file, line) :=
    EX(tcid,
            IndirectTestId(tcid, nptu_x) &
            tres = "INDIRECT_TEST_TRESHOLD" &
            TestEntityInfo(tcid, tcnm, file, line)
      );

PRINT ["IndirectTest"] IndirectTest(aa,bb,cc,dd,ee);
