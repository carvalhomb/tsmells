/**
 * This file is part of TSmells
 *
 * TSmells is free software; you can redistribute it and/or modify it 
 * under the terms of the GNU General Public License as published by the 
 * Free Software Foundation; either version 2 of the License, or (at your 
 * option) any later version.
 *
 * TSmells is distributed in the hope that it will be useful, but WITHOUT 
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS 
 * FOR A PARTICULAR PURPOSE. See the GNU General Public License for more 
 * details.
 *
 * You should have received a copy of the GNU General Public License along 
 * with TSmells; if not, write to the Free Software Foundation, Inc., 
 * 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA 
 *
 * Copyright 2007 Manuel Breugelmans <manuel.breugelmans@student.ua.ac.be>
 */

/**
 * Detection of 'toString' in test commands and helpers
 **/

// TODO parameterize this with a blacklist
ToStringId(x):=EX(y,Method(x,y) & @"[.]toString()"(y));

/**
 * To string invocations of a testmethod {command,helper}
 *
 * @invId: invocation ID
 * @tmId:  testmethod ID
 **/
ToStringInv(invId, tmId) := 
    EX(toStr,
        PureInvokes(invId,tmId,toStr) & 
        (TestCommandId(tmId) | TestHelperId(tmId)) &
        ToStringId(toStr)
      );

/**
 * Number of toString invocations in a test command or helper
 *
 * @col x: test method ID
 * @col y: nrof toStrings
 **/
NrofToStringInv("","");

FOR tmId IN (TestCommandId(x)|TestHelperId(x)) {
    nrof := #(ToStringInv(x,tmId));
    NrofToStringInv(id_, nr) :=
        NrofToStringInv(id_, nr) |
        ( id_ = tmId & count(nr, STRING(nrof)));
}

//PRINT NrofToStringInv(a,b);
Empty("TC");

SensitiveEquality(e, tm, nrof_, file_, line_) :=
    EX(tmId_,
        Empty(e) &
        TestEntityInfo(tmId_, tm, file_, line_) &
        NrofToStringInv(tmId_, nrof_) &
        !count(nrof_, "0")
    );

PRINT ["SensitiveEquality"] SensitiveEquality(aa,bb,cc,dd,ee);
PRINT "## NrofToStrings ##",ENDL,"method name;# tostring invocations",ENDL TO "DUMP_TEST_METRICS";
PRINT EX(x, Method(x, n) & NrofToStringInv(x, nrof_)) TO "DUMP_TEST_METRICS";

